plugins {
    id 'dev.architectury.loom' version '1.7-SNAPSHOT' apply false
    id 'architectury-plugin' version '3.4-SNAPSHOT'
    id 'com.github.johnrengelman.shadow' version '8.1.1' apply false
    id "com.hypherionmc.modutils.modpublisher" version "2.1.6" apply false
    id "co.uzzu.dotenv.gradle" version "4.0.0"
}

architectury {
    minecraft = project.minecraft_version
}

allprojects {
    group = rootProject.maven_group
    version = rootProject.mod_version
}

subprojects {
    apply plugin: 'dev.architectury.loom'
    apply plugin: 'architectury-plugin'
    apply plugin: 'maven-publish'
    apply plugin: "com.hypherionmc.modutils.modpublisher"

    base {
        archivesName = "$rootProject.archives_name-$project.name"
    }

    repositories {}

    dependencies {
        minecraft "net.minecraft:minecraft:$rootProject.minecraft_version"
        mappings "net.fabricmc:yarn:$rootProject.yarn_mappings:v2"
    }

    java {
        withSourcesJar()

        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.release = 17
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifactId = base.archivesName.get()
                from components.java
            }
        }

        repositories {}
    }

    publisher {
        apiKeys {
            curseforge env.CURSE_TOKEN.value
            modrinth env.MODRINTH_TOKEN.value
        }

        setCurseID rootProject.curseforge_id
        setModrinthID rootProject.modrinth_id

        setVersionType "release"
        setDisplayName "[$rootProject.minecraft_version - ${project.name.capitalize()}] Advancement No More - $rootProject.mod_version"
        setGameVersions rootProject.minecraft_version
        setLoaders([project.name])
        setCurseEnvironment "both"
        setArtifact remapJar
        setJavaVersions([java.targetCompatibility])
        setChangelog "Fixed the Mod not having the Mod"
    }
}
